prototype(Neos.Presentation:Module.HighlightBox) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = "Highlight Box"
        props {
            headline.text = 'Meet the <strong>Community</strong>'
            description.text = 'The Neos Team consists of many independent individuals who are actively contributing to Neos. Each has their own focus and specialty. Together with the Neos community they push the project forward. This includes the organisation of Neos events, contributing code, writing documentation, managing the websites & services used by the community and much more.'
            image.imageSource = Sitegeist.Kaleidoscope:UriImageSource {
                uri = 'https://source.unsplash.com/random/800x600'
            }
            button.text = 'More information'
        }
    }

    @propTypes {
        image = PropTypes:DataStructure {
            imageSource = ${PropTypes.instanceOf('\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface')}
        }
        headline = PropTypes:FromPrototype {
            prototypeName = "Neos.Presentation:Headline"
        }
        description = PropTypes:FromPrototype {
            prototypeName = "Neos.Presentation:Paragraph"
        }
        imageRight = PropTypes:Bool
    }

    imageRight = false

    renderer = afx`
        <Neos.Presentation:Spacing size y>
            <div class={['flex items-center', props.imageRight ? 'flex-row-reverse' : null]}>
                <Neos.Presentation:Spacing x>
                    <Sitegeist.Kaleidoscope:Image {...props.image}/>
                </Neos.Presentation:Spacing>
                <Neos.Presentation:Spacing y="small">
                    <Neos.Presentation:Spacing y="small">
                        <Neos.Presentation:Headline {...props.headline} display="headline-lg"/>
                    </Neos.Presentation:Spacing>
                    <Neos.Presentation:Spacing y="small">
                        <Neos.Presentation:Paragraph {...props.description} display="lead"/>
                    </Neos.Presentation:Spacing>
                    <Neos.Presentation:Spacing y="small">
                        <Neos.Presentation:Button {...props.button}/>
                    </Neos.Presentation:Spacing>
                </Neos.Presentation:Spacing>
            </div>
        </Neos.Presentation:Spacing>
    `
}
