prototype(Neos.Presentation:Navigation) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = "Navigation"
        container = Neos.Presentation:Background {
            variant = 'gradient'
            content = ${value}
        }
        props {
            links = Neos.Fusion:DataStructure {
                0 {
                    label = "Product"
                    link = "#"
                    flyoutItems = Neos.Presentation:Navigation.Flyout {
                        title = "Product"
                        column0 = afx`
                            <ul>
                                <li>Item a</li>
                                <li>Item a</li>
                                <li>Item a</li>
                            </ul>
                        `
                    }
                }
                1 {
                    label = "Cases"
                    link = "#"
                    flyoutItems = Neos.Presentation:Navigation.Flyout {
                        title = "Cases"
                        column0 = afx`
                            <ul>
                                <li>Item a</li>
                                <li>Item a</li>
                                <li>Item a</li>
                            </ul>
                        `
                        column1 = afx`
                            <ul>
                                <li>Item b</li>
                                <li>Item b</li>
                                <li>Item b</li>
                            </ul>
                        `
                        column2 = afx`
                            <ul>
                                <li>Item c</li>
                                <li>Item c</li>
                                <li>Item c</li>
                            </ul>
                        `
                    }
                }
                2 {
                    label = "Community"
                    link = "#"
                }
                3 {
                    label = "Download"
                    link = "#"
                }
                4 {
                    label = "Blog"
                    link = "#"
                }
            }
        }
    }

    @propTypes {
        links = PropTypes:Array {
            type = PropTypes:Union {
                groups = PropTypes:DataStructure {
                    label = ${PropTypes.string.required}
                    link = ${PropTypes.string}
                    flyoutItems = ${PropTypes.string}
                }
            }
        }
    }

    renderer = afx`
        <Neos.Presentation:Spacing tagName="nav" size x y="small">
            <ul class="flex justify-between items-center gap-x-6">
                <Neos.Presentation:Icon name="neos-white-logo" class="h-10" />
                <div x-data="{linkInFlyout:null}" class="hidden lg:flex items-center gap-5 2xl:gap-10" "x-on:click.away"='linkInFlyout = null'>
                    <Neos.Fusion:Loop items={props.links} itemName="link">
                        <li class="group">
                            <a
                               "x-on:click.preventDefault"={'linkInFlyout === ' + iterator.index + ' ? linkInFlyout = null : linkInFlyout = ' + iterator.index}
                               href={link.link}
                               class="inline-flex items-center gap-1 hover:bg-white/10 px-4 py-1 rounded-md display-lead"
                            >
                                {link.label}
                                <Neos.Presentation:Icon
                                        @if={link.flyoutItems && link.flyoutItems != []}
                                        name="chevron-down"
                                        class="h-6"
                                />
                            </a>
                            <div
                                x-show={'linkInFlyout === ' + iterator.index}
                                x-cloak
                                class="absolute left-0 w-full"
                                @if={link.flyoutItems}
                            >
                                {link.flyoutItems}
                            </div>
                        </li>
                    </Neos.Fusion:Loop>
                </div>
                <Neos.Presentation:Button text="Try Neos" />
            </ul>
        </Neos.Presentation:Spacing>
    `
}
